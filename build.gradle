buildscript {
  ext {
    kotlin_version = '1.1.4-2'
    vertx_version = '3.4.2'
  }

  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'kotlin'

group 'com.technogi'
version '0.0.15'
sourceCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'

def mainVerticleName = 'com.technogi.rdeb.server.HttpServer'

repositories {
  jcenter()
  maven { url 'https://jitpack.io' }

}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile "com.github.Technogi:rdeb-client-java:${version}"

  compile "io.vertx:vertx-core:${vertx_version}"
  compile "io.vertx:vertx-web:${vertx_version}"
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'


}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

run {
  args = ['run', mainVerticleName,
          "--launcher-class=$mainClassName",
          "--redeploy=src/**/*.*",
          "--on-redeploy=./gradlew classes",
          "-conf 'conf/rdeb-conf.json'"
  ]
}

shadowJar {
  baseName = 'rdeb-server'
  classifier = 'shadow'

  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

// Heroku relies on the 'stage' task to deploy.
task stage {
  dependsOn shadowJar
}